startshape Scene

CF::Background = [b -1]
CF::Size = [s 14 x -4 y -4]

margin = 0.025
w = 0.02

shape Scene {
	Grid [b 1 x 4 y 4]
	loop 5 [x 2] {
	loop 5 [y 2] {
	Ball[ z 1]
	}
	}
}

shape testScene {
	Grid [b 1 x 4 y 4]
}

path Ring {
	MOVETO(1,0)
	ARCTO(-1,0,1)
	ARCTO(1,0,1)
	CLOSEPOLY()
	STROKE(w) []
}

shape Bars
rule 5 {
	n=randint(4)^2+randint(3)
	loop i=n [] {
		SQUARE [x (-1 + i/n) s w (2*sqrt(1-((i-n)/n)^2))]
	}
}

shape Grid {
	n = randint(5)*2+4
	m = randint(5)*2 + 4
	loop n+1 [x 1]
	SQUARE[s w (m+w) x (-(n)/2) ]
	loop m+1 [y 1]
	SQUARE[s n w y (-(m)/2)]

	i = n+m
	loop i [] {
		Block(n,m) [z 0.1]
	}

	loop n/2+m/2 [] Dot(n,m) [ z 0.8]
	loop n/4+m/4 [] Connector(n,m) [z 0.2]
}

shape Dot(number n, number m)  {
	CIRCLE[x randint(-n/2,n/2+1) 
				y randint(-m/2,m/2+1)
				s 0.2]
}

shape Connector(number n, number m)  {
	
	dn = randint(4)+1
	dm = randint(4)+1
	transform [ s ((-1)^randint(2)) ((-1)^randint(2))]{
	transform [x (randint(n-dn+1)) y (randint(m-dm+1))] {
	SQUARE [[x (-n/2) y (-m/2) r (-atan(dn/dm)) s w (sqrt(dn^2+dm^2)) y 0.5 ]]
	}
	}
}

shape Block(number n, number m) {
	SQUARE [s (2-w-margin) b -1
					x (randint(n)-n/2)
					y (randint(m)-m/2)]
}

shape Ball 
rule {
	Ring[b 1]
	Bars[b 1 r (randint(4)*90)]
}
rule {
	Ring[b 1]
	Bars[b 1 r (randint(4)*90)]
	CIRCLE[s 2 b -1 z -0.5]
} 
rule {
	
	Bars[b 1 r (randint(4)*90)]
	
} 
rule 4 {}
rule 0.4{
	Ring[b 1]
	
	CIRCLE[s 2 b -1 z -0.1 b 0.3 a -0.4]
} 
rule  0.4{
	Ring[b 1]
	
	CIRCLE[s 2 b -1 z -0.1 b 0.3 a -0.4]
	CIRCLE[s 2 b -1 z -0.8]
} 

//HGK HHB  AHQ AIL
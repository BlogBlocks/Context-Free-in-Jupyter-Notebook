startshape scene { color } 

rule scene { 
rectangle { s 0.7072 1} 
# second line for fewer rectangles 
rectangle { r 90 s 0.7072 1} 
} 

rule rectangle { 
# alpha for texture 
rectangle [ r 89 s 0.7072 y 0.49 alpha -0.2 ] 
rectangle [ r -92 s 0.7072 y 0.49 alpha 0.05] 
} 

rule rectangle { 
rectangle [ r -89 s 0.7072 y 0.49 alpha -0.2] 
rectangle [ r 92 s 0.7072 y 0.49 alpha 0.05] 
} 

rule rectangle 0.3 { 
   # draw a rectangle 
   FILL { s 1.4142 1 } 
   # and continue with a lighter texture 
   rectangle { alpha -0.9 hue -30 sat -0.5 b 0.5} 
} 

rule rectangle 0.3 { 
   # draw a rectangle 
   FILL { s 1.4142 1 } 
   # and continue with texture 
   rectangle { alpha -0.2 } 
} 

rule rectangle 0.1 { 
   # draw a rectangle 
   FILL { s 1.4142 1 } 
} 

# remaining is genuine kapowaz 

rule FILL { HSQUARE { size 1 } } 

// apply variable hue 
rule HSQUARE { SSQUARE { hue 100 } } 
rule HSQUARE { SSQUARE { hue 110 } } 
rule HSQUARE { SSQUARE { hue 120 } } 
rule HSQUARE { SSQUARE { hue 130 } } 
rule HSQUARE { SSQUARE { hue 140 } } 
rule HSQUARE { SSQUARE { hue 150 } } 

// apply variable saturation 
rule SSQUARE { BSQUARE { sat 0 } } 
rule SSQUARE { BSQUARE { sat 0.2 } } 
rule SSQUARE { BSQUARE { sat 0.4 } } 
rule SSQUARE { BSQUARE { sat 0.6 } } 
rule SSQUARE { BSQUARE { sat 0.9 } } 
rule SSQUARE { BSQUARE { sat 1 } } 
rule SSQUARE { BSQUARE { sat -0.2 } } 
rule SSQUARE { BSQUARE { sat -0.4 } } 

// apply variable brightness 
rule BSQUARE { SQUARE { b 0 } } 
rule BSQUARE { SQUARE { b 0.2 } } 
rule BSQUARE { SQUARE { b 0.4 } } 
rule BSQUARE { SQUARE { b 0.6 } } 
rule BSQUARE { SQUARE { b 0.8 } } 
rule BSQUARE { SQUARE { b 1 } }
